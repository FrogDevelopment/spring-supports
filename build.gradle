plugins {
    id 'io.freefair.lombok' version '6.2.0' apply false
    id 'com.google.cloud.tools.jib' version '3.1.4' apply false
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1' apply false
    id 'org.springframework.boot' version '2.6.6' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.google.cloud.tools.jib'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.frog-development'

    version = '2020.0.4'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$version"
            mavenBom 'org.junit:junit-bom:5.8.1'
        }
    }

    dependencies {
        def dockerSecretsVersion = '1.0.0'

        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        implementation "com.frog-development:docker-secrets-module:$dockerSecretsVersion"

        implementation 'org.jolokia:jolokia-core'
        implementation 'net.logstash.logback:logstash-logback-encoder:6.6'
        implementation 'com.github.ben-manes.caffeine:caffeine:3.0.4'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        testRuntimeOnly 'org.junit.platform:junit-platform-runner'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    springBoot {
        buildInfo()
    }

    test {
        reports.html.required = false

        useJUnitPlatform {
        }
    }

    jib {
        from {
            image = 'eclipse-temurin:17-focal'
        }
        to {
            image = "springsupports/support-${project.name}"
            tags = [version.toString()]
        }
        container {
            jvmFlags = ['-Xmx128m']
            ports = ['8080']
            volumes = ["/tmp"]
            creationTime = 'USE_CURRENT_TIMESTAMP'
            labels = [
                    'frog.image_base': jib.from.image,
                    'spring-cloud.version'   : version.toString(),
                    'spring-boot.version'   : '2.5.6',
                    'jdk':'17'
            ]
        }
    }
}

wrapper {
    gradleVersion = '7.2'
    distributionType = Wrapper.DistributionType.ALL
}
